use rchip8::game8::ast::*;
use std::str::FromStr;
use std::rc::Rc;

grammar;

Ident: String = r"[a-zA-Z][a-zA-Z0-9_]*" => String::from(<>);

CommaList<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
}

Num: u16 = {
    r"[0-9]+" => u16::from_str(<>).unwrap(),
    r"0x[0-9a-fA-F]+" => u16::from_str_radix(&<>[2..], 16).unwrap(),
};

Term: Rc<Expression> = {
    Num => Rc::new(Expression::Number(<>)),
    Ident => Rc::new(Expression::Variable(<>)),
    <Ident> "(" <CommaList<Expr>> ")" => Rc::new(Expression::FuncCallExpr(<>)),
    "(" <Expr> ")" => <>,
};

MulExpr: Rc<Expression> = {
    <x:MulExpr> "*" <y:Term> => Rc::new(Expression::BinOp(BinOperator::MUL, x, y)),
    <x:MulExpr> "/" <y:Term> => Rc::new(Expression::BinOp(BinOperator::DIV, x, y)),
    Term,
};

AddExpr: Rc<Expression> = {
    <x:AddExpr> "+" <y:MulExpr> => Rc::new(Expression::BinOp(BinOperator::ADD, x, y)),  
    <x:AddExpr> "-" <y:MulExpr> => Rc::new(Expression::BinOp(BinOperator::SUB, x, y)),  
    MulExpr,
};

CmpExpr: Rc<Expression> = {
    <x:CmpExpr> "==" <y:AddExpr> => Rc::new(Expression::BinOp(BinOperator::EQL, x, y)),
    <x:CmpExpr> "!=" <y:AddExpr> => Rc::new(Expression::BinOp(BinOperator::NEQ, x, y)),
    <x:CmpExpr> "<=" <y:AddExpr> => Rc::new(Expression::BinOp(BinOperator::LTE, x, y)),
    <x:CmpExpr> "<" <y:AddExpr> => Rc::new(Expression::BinOp(BinOperator::LT, x, y)),
    <x:CmpExpr> ">=" <y:AddExpr> => Rc::new(Expression::BinOp(BinOperator::GTE, x, y)),
    <x:CmpExpr> ">" <y:AddExpr> => Rc::new(Expression::BinOp(BinOperator::GT, x, y)),
    AddExpr
}

Expr: Rc<Expression> = CmpExpr;

Stmt: Rc<Statement> = {
    "if" <Expr> "then" <Stmt+> "endif" => Rc::new(Statement::IfThen(<>)),
    "if" <Expr> "then" <Stmt+> "else" <CommaList<Stmt>> "endif" => 
        Rc::new(Statement::IfElse(<>)),
    "while" <Expr> "do" <Stmt+> "endwhile" => Rc::new(Statement::While(<>)),
    <Ident> "(" <CommaList<Ident>> ")" ";" => Rc::new(Statement::FuncCallStmt(<>)), 
    "let" <Ident> "=" <Expr> ";" => Rc::new(Statement::Assign(<>)),
    "return" <Expr> ";" => Rc::new(Statement::Return(<>)),
}

FuncDef: FuncDefinition = {
    "def" <Ident> "(" <CommaList<Ident>> ")" "do" <Stmt+> "enddef" => FuncDefinition(<>),
};

pub Prog: Program = FuncDef* Stmt+ => Program(<>);